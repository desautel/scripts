#!/usr/bin/perl -w
# -*- perl -*-

=head1 NAME

snmp__cisco_portstatus - Munin plugin to monitor port status of cisco switch

=head1 CONFIGURATION

The following configuration variables are used

 host      - SNMP host to contact (default taken from link name)
 port      - SNMP port to use (default 161)
 community - SNMP community string to use (default "public")

=head1 LICENSE

MPL/GPL

=cut

use strict;
use Munin::Plugin::SNMP;

# oid
my $oidBase  = '1.3.6.1.2.1.2.2.1';

if (defined $ARGV[0] and $ARGV[0] eq "snmpconf")
{
    print "require $oidBase [2,5,7,8]\n";
    exit 0;
}

my $session = Munin::Plugin::SNMP->session();
my (%info, $cid);
my $oidCols = {
                2 => 'name',
                3 => 'type',
                5 => 'speed',
                7 => 'admstatus',
                8 => 'operstatus',
};

if (defined $ARGV[0] and $ARGV[0] eq "config")
{
    my ($host) = Munin::Plugin::SNMP->config_session();
    print "host_name $host\n";
    print "graph_title Cisco Port status\n";
    print "graph_category system\n";
    print "graph_vlabel %\n";
    print "graph_info Port status\n";
	print "graph_args --base 1000 -l 0\n";

    my $name;
    if (&get_multi_composite($session, $oidBase, $oidCols, \%info) != 0) {
        foreach (keys %info) {
            if (defined($info{$_}->{name}) &&
                (defined($info{$_}->{type}) && $info{$_}->{type} == 6)) {
                $name = $info{$_}->{name};
                print "i${_}_speed.label $name Link speed in exp10 Mbps\n";
                print "i${_}_speed.type GAUGE\n";
                print "i${_}_admstat.label $name Admin status\n";
                print "i${_}_admstat.type GAUGE\n";
                print "i${_}_operstat.label $name Operational status\n";
                print "i${_}_operstat.type GAUGE\n";
            }
        }
    }
    exit 0;
}

#                2 => 'name',
#                5 => 'speed',
#                7 => 'admstatus',
#                8 => 'operstatus',
if (&get_multi_composite($session, $oidBase, $oidCols, \%info) != 0) {
    foreach (keys %info) {
        if (defined($info{$_}->{name}) &&
            (defined($info{$_}->{type}) && $info{$_}->{type} == 6)) {
            if (($info{$_}->{speed} > 0) &&
                (log($info{$_}->{speed})/log(10) > 6)) {
                print "i${_}_speed.value " . (log($info{$_}->{speed})/log(10) - 6) . "\n";
            } else {
                print "i${_}_speed.value 0\n";
            }
            print "i${_}_admstat.value " . $info{$_}->{admstatus} . "\n";
            print "i${_}_operstat.value " . $info{$_}->{operstatus} . "\n";
        }
    }
}


exit;

sub get_multi_composite {
    my ($session, $oidBase, $oidCols, $snmpinfo) = @_;
    my $ret = $session->get_hash(
            -baseoid => $oidBase,
            -cols    => $oidCols);
    if (! defined($ret)) {return 0; }
    my ($ckey, $corig);
    foreach $corig (keys %$ret) {
        $corig =~ /(\d+)/;
        $ckey = $1;
        if (! defined($snmpinfo->{$ckey})) {$snmpinfo->{$ckey} = {}; }
        foreach (keys %{$ret->{$corig}}) {
            $snmpinfo->{$ckey}->{$_} = $ret->{$corig}->{$_};
        }
    }
    return 1;
}


sub get_numeric {
    my ($val) = @_;
    if ($val =~ /^([0-9\.]+)/) {return $1; }
    return undef;
}

sub get_single {
    my ($session, $oid) = @_;
    my $res = $session->get_request($oid);
    my $val = undef;
    if ($session->error_status) {return undef; }
    # treat only if value returned, unless not touch $val
    if (defined($res->{$oid})) {
        if ($res->{$oid} ne 'noSuchObject') {$val = $res->{$oid}; }
    } elsif (defined($res->{$oid . '.0'})) {
        if ($res->{$oid . '.0'} ne 'noSuchObject') {$val = $res->{$oid . '.0'}; }
    } else {
    }
    return $val;
}

